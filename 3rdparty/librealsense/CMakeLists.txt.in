cmake_minimum_required(VERSION 3.11)

project(librealsense_)

include(ExternalProject)

set(LIBREALSENSE_VERSION "2.39.0")
set(LIBREALSENSE_CMAKE_ARGS
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_BUILD_TYPE=Release
    -DBUILD_SHARED_LIBS=OFF
    -DBUILD_EXAMPLES=OFF
    -DBUILD_UNIT_TESTS=OFF
    -DBUILD_GLSL_EXTENSIONS=OFF
    -DBUILD_GRAPHICAL_EXAMPLES=OFF
    -DBUILD_PYTHON_BINDINGS=@BUILD_PYTHON_MODULE@
    -DBUILD_WITH_CUDA=@BUILD_CUDA_MODULE@
    )
if (CMAKE_SYSTEM_NAME STREQUAL "Darwin")
    list(APPEND LIBREALSENSE_CMAKE_ARGS
        -DBUILD_WITH_OPENMP=OFF
        -DHWM_OVER_XU=OFF)
elseif (CMAKE_SYSTEM_NAME STREQUAL "Windows")
    list(APPEND LIBREALSENSE_CMAKE_ARGS
        -DBUILD_WITH_STATIC_CRT=@STATIC_WINDOWS_RUNTIME@)
endif ()

ExternalProject_Add(
    ext_librealsense
    PREFIX @CMAKE_BINARY_DIR@/3rdparty/librealsense
    URL "https://github.com/IntelRealSense/librealsense/archive/v${LIBREALSENSE_VERSION}.zip"
    URL_HASH SHA256=64ca0c9eed0dce23bfe2502c7c8d02f71d0534e3cbe4e2c888380ccfb06d7970
    CMAKE_ARGS ${LIBREALSENSE_CMAKE_ARGS}
    TEST_COMMAND ""
)
